 Test←{
     t←()
    ⍝ Tests ability to navigate sharp, narrow valleys and handle situations where the Jacobian
    ⍝ may lead to a locally rank-deficient J^T J matrix (eg. zero diagonal elements), requiring
    ⍝ robust damping
     t.Beale←{
         s←R{
             x y←⍵ ⋄ (1.5 2.25 2.625-x×1-y*⍳3)[(¯1+y)x ⋄ (¯1+y*2)(2×x×y) ⋄ (¯1+y*3)(3×x×y*2)]
         }#.LMA(1 1)0.000001 ⋄ 3 0.5 CMP s.p
     }
    ⍝ Assesses capability to follow a long, narrow, curving multi-dimensional valley, testing
    ⍝ the interplay between step length and direction adjustments controlled by the damping factor
     t.Helical←{
         T←{1|1+(12○⍺+0J1×⍵)÷○2} ⋄ M←{|⍺+0J1×⍵}
         H←{
             a b c←⍵ ⋄ m←a M b ⋄ y←(10×c-10×a T b)(10×m-1)c
             y[((50×b)÷○+.×⍨a b)(-(50×a)÷○+.×⍨a b)10 ⋄ (10×a÷m)(10×b÷m)0 ⋄ 0 0 1]
         }
         r←1 0 0 CMP(R H #.LMA ¯1 0 0).p
         r,←1 0 0 CMP(R H #.LMA ¯1.2 0.1 0.1).p
         r,←1 0 0 CMP(R H #.LMA ¯0.9 ¯0.05 ¯0.05).p
         r,←1 0 0 CMP(R H #.LMA 0.5 0.5 0.5).p
         r,←1 0 0 CMP(R H #.LMA ¯0.5 ¯0.5 ¯0.5).p
         r
     }
    ⍝ Verifies efficiency and correctness for a linear system. Converge shoud be very fast
    ⍝ (1 or 2 iterations) with minimal damping, demonstrating Gauss-Newton like behavior
     t.Linear←{
         y←(A←?100 10⍴0)+.×x←⍳10 ⋄ s←R{(y-⍨A+.×⍵)A}#.LMA(?10⍴0)0 ⋄ (s.iter<10),(⍳10)CMP s.p
     }
    ⍝ Evaluates LMA's robustness and ability to converge to a solution when the Jacobian
    ⍝ becomes singular (rank-deficient) at or near the optimum
     t.Powell←{
         s←R{a b c d←⍵ ⋄ r5 r10←5 10*÷2 ⋄ y←(a+10×b)(r5×c-d)(×⍨b-2×c)(r10××⍨a-d)
             y[1 10 0 0 ⋄ 0 0 r5(-r5) ⋄ 0(2×b-2×c)(-2×b-2×c)0 ⋄ (2×r10×a-d)0 0(-2×r10×a-d)]
         }#.LMA(3 ¯1 0 1)(tols:1E¯30) ⋄ (4⍴0)CMP s.p
     }
    ⍝ Tests LMA's performance in minimizing a classic non-linear function characterized by
    ⍝ a deep, narrow, banana-shaped valley, requiring effective adaptation of search
    ⍝ direction and step size
     Rosenbrock←{p q←⍵ ⋄ ((10×q-×⍨p),1-p)[(-20×p)10 ⋄ ¯1 0]}
     t.Rosenbrock←{
         s←R #.Rosenbrock #.LMA(0 0) ⋄ 1 1 CMP s.p
     }
    ⍝ Check that algorithm finishes on termination conditions
     t.Terminate←{
         ti tr←10 1E¯10
         r←(R #.Rosenbrock #.LMA(0 0)(tolr:0)).(iter>toli)               ⍝ number of iterations
         r,←(R #.Rosenbrock #.LMA(0 0)(tols:0)).(rel<tolr)               ⍝ relative change
         r,←(R #.Rosenbrock #.LMA(0 0)(tolr:0 ⋄ dmax:1000000)).(dnorm>1000000)   ⍝ maximum damping
         r
     }
     fn←↓t.⎕NL 3
     ⍺←0.000001 ⋄ tol←⍺ ⋄ CMP←{tol>|⍺-⍵}
     R←{⎕←(10↑''),⍵.((6 0⍕iter),(12 ¯5⍕sse),p) ⋄ ⍵}
     t←t ⎕NS'tol' 'CMP' 'R'
     {⎕←⍵ ⋄ 0∊t⍎⍵,'⍬':⎕SIGNAL 8 ⋄ _←0}¨fn
 }
